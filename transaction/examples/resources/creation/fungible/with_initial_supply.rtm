# This transaction manifest creates a new fungible resource with initial supply and with the default
# auth.

# ==================================================================================================
# WARNING: If you will be submitting this transaction through the Babylon wallet then you MUST
# remove the "lock_fee" instruction that you see below. Otherwise your transaction will fail. If all 
# that you are using is resim then you can safely ignore this warning.
# ==================================================================================================

# Locking 10 XRD in fees from the account component. 
CALL_METHOD 
    Address("${account_address}") 
    "lock_fee"
    Decimal("500");

# Creating a new resource with a divisibility of 18 and a name of `MyResource`. The resource has 
# default resource behavior where it can be withdrawn and deposited by anybody.
CREATE_FUNGIBLE_RESOURCE_WITH_INITIAL_SUPPLY
    Enum<0u8>()
    false # Track Total Supply
    18u8
    Decimal("${initial_supply}")
    # This tuple of enums defines the modifiable behaviors of the resource. Each element in
    # the tuple defines a different resource behavior. As an example, the first element in this tuple
    # defines that the resource is mintable.
    Tuple(
        Enum<Option::Some>(                                                     # Specify Mint Roles
            Tuple(
                Tuple(Enum<Option::Some>(Enum<AccessRule::AllowAll>()), true),  # Minter Role
                Tuple(Enum<Option::Some>(Enum<AccessRule::DenyAll>()), true)    # Minter Updater Role
            )
        ),
        Enum<0u8>(),                                                            # Use Default Burn Roles
        Enum<0u8>(),                                                            # Use Default Freeze Roles
        Enum<0u8>(),                                                            # Use Default Recall Roles
        Enum<0u8>(),                                                            # Use Default Withdraw Roles
        Enum<0u8>()                                                             # Use Default Deposit Roles
    )
    Tuple(
        Map<String, Tuple>(
            "name" => Tuple(
                Enum<Option::Some>(
                    Enum<Metadata::String>("MyResource")                               # Resource Name
                ),
                true                                                                   # Locked Resource Name
            ),
            "symbol" => Tuple(
                Enum<Option::Some>(
                    Enum<Metadata::String>("RSRC"),                                   # Resource Symbol
                ),
                true                                                                   # Locked Resource Symbol
            ),
            "description" => Tuple(
                Enum<Option::Some>(
                    Enum<Metadata::String>("A very innovative and important resource")    # Resource Description
                ),
                true                                                                      # Locked Resource Description
            )
        ),
        Map<String, Tuple>()
    )
    Enum<0u8>();                                                                           # No Address Reservation

# Depositing the entirety of the initial supply of the newly created resource into our account 
# component.
CALL_METHOD
    Address("${account_address}") 
    "deposit_batch"
    Expression("ENTIRE_WORKTOP");