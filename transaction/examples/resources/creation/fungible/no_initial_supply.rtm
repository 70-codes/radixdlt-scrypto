# This transaction manifest creates a new fungible resource with no initial supply and with the 
# default auth.

# ==================================================================================================
# WARNING: If you will be submitting this transaction through the Babylon wallet then you MUST
# remove the "lock_fee" instruction that you see below. Otherwise your transaction will fail. If all 
# that you are using is resim then you can safely ignore this warning.
# ==================================================================================================

# Locking 10 XRD in fees from the account component. 
CALL_METHOD 
    Address("${account_address}") 
    "lock_fee"
    Decimal("10");

# Creating a new resource with a divisibility of 18 and a name of `MyResource`. The resource has 
# default resource behavior where it can be withdrawn and deposited by anybody.
CREATE_FUNGIBLE_RESOURCE
    Enum<0u8>()
    false # Track Total Supply
    18u8
    # This array of enums defines the modifiable behaviors of the resource. Each element in
    # the array defines a different resource behavior. As an example, the first element in this array
    # defines that the resource is mintable.
    Array<Enum>(Enum<ResourceAction::Mint>())
    # This map defines the authorization rules for a given role which maps to a set of methods.
    # In this example, the "minter", a role which is able to mint new tokens, is given permission
    # to everyone. This rule is also locked and permanently cant be changed.
    Map<String, Tuple>(
        "minter" => Tuple(Enum<1u8>(Enum<AccessRule::AllowAll>()), true)
    )
    Tuple(
        Map<String, Tuple>(
            "name" => Tuple(
                Enum<Option::Some>(
                    Enum<Metadata::String>("MyResource")                               # Resource Name
                ),
                true                                                                   # Locked Resource Name
            ),
            "symbol" => Tuple(
                Enum<Option::Some>(
                    Enum<Metadata::String>("RSRC"),                                   # Resource Symbol
                ),
                true                                                                   # Locked Resource Symbol
            ),
            "description" => Tuple(
                Enum<Option::Some>(
                    Enum<Metadata::String>("A very innovative and important resource")    # Resource Description
                ),
                true                                                                      # Locked Resource Description
            )
        ),
        Map<String, Tuple>()
    )
    Enum<0u8>();                                                                         # No Address Reservation