# This transaction manifest creates a new non-fungible resource with no initial supply and with the 
# default auth.

# ==================================================================================================
# WARNING: If you will be submitting this transaction through the Babylon wallet then you MUST
# remove the "lock_fee" instruction that you see below. Otherwise your transaction will fail. If all 
# that you are using is resim then you can safely ignore this warning.
# ==================================================================================================

# Locking 10 XRD in fees from the account component. 
CALL_METHOD 
    Address("${account_address}") 
    "lock_fee"
    Decimal("500");

# Creating a new resource 
CREATE_NON_FUNGIBLE_RESOURCE
    Enum<0u8>()
    Enum<NonFungibleIdType::Integer>()
    false # Track Total Supply
    Tuple(Tuple(Array<Enum>(), Array<Tuple>(), Array<Enum>()), Enum<0u8>(64u8), Array<String>())
    # This tuple of enums defines the modifiable behaviors of the resource. Each element in
    # the tuple defines a different resource behavior. As an example, the first element in this tuple
    # defines that the resource is mintable.
    Tuple(
        Enum<1u8>(
            Tuple(
                Tuple(
                    Enum<1u8>(
                        Enum<0u8>()
                    ),
                    true
                ),
                Tuple(
                    Enum<1u8>(
                        Enum<1u8>()
                    ),
                    true
                )
            )
        ),
        Enum<0u8>(),
        Enum<0u8>(),
        Enum<0u8>(),
        Enum<0u8>(),
        Enum<0u8>()
    )
    Tuple(
        Map<String, Tuple>(
            "name" => Tuple(
                Enum<Option::Some>(
                    Enum<Metadata::String>("MyResource")                               # Resource Name
                ),
                true                                                                   # Locked Resource Name
            ),
            "description" => Tuple(
                Enum<Option::Some>(
                    Enum<Metadata::String>("A very innovative and important resource")    # Resource Description
                ),
                false
            )
        ),
        Map<String, Tuple>()
    )
    Enum<0u8>();